       TITL 'CHAIN - CHAIN PROGRAM ROUTINE'
       IDT  'CHAIN'
*
*  THIS ROUTINE IS CALLED TO CALL ANOTHER PROGRAM FOR EXECUTION.
*
*  IN GPL (TI/99) MODE, THE ROUTINE STORES THE CALLED FILEID IN THE
*  MENU SAVFIL PAB, AND THEN CALLS THE MENU ROUTINE TO LOAD AND
*  RUN THE NEW PROGRAM.   THE GPL VERSION IS NOT MULTITASKING.
*
*  IN MDOS (GENEVE) MODE, THE ROUTINE PARSES THE FILE NAME USING THE
*  UTILITY XOP, AND THEN CALLS THE MDOS LOADER TO LOAD AND RUN THE
*  PROGRAM.  THE MDOS VERSION IS MULTITASKING.
*
*  CALLING SEQUENCE:
*
*      CALL CHAIN ( FILEID, ERROR )
*
*  WHERE:
*
*  FILEID  -  IS AN INTEGER ARRAY WHICH DESCRIBES THE FILE NAME.
*	      IT IS TERMINATED BY A BLANK CHARACTER (ASCII >20).
*
*  ERROR   -  IS A ONE-WORD INTEGER VARIABLE WHICH DEFINES ANY
*	      ERROR.
*
*  Change History:
*
*  	V3.0  19-Aug-87:   Initial Version
*  	V4.0  02-Oct-88:   MDOS Compatible Version
*  	v4.2  25-Feb-89:   Fixes per Clint Pulley, Thanks, Clint!
*       V4.4  08-AUG-89:   FIX GPL VERSION
*
*  Copyright 1989 by LGMA Products
*
*  DEFINITIONS:
*
	DEF  CHAIN         CHAIN FILE ROUTINE
*
	COPY "FORTPAR:"
	UNL
	COPY "EQUATES:FORTEX.EQ9"
	IF   GENEVE
	COPY "EQUATES:UTIL.EQ9"
LOWRAM	EQU  >0410		A LOW RAM PLACE TO STORE FILE NAME
	ENDIF
	LIST
*
*  MAIN LOGIC START:
*
CHAIN   EQU  $
	DATA -2			2 ARGUMENTS FOR TI-99
	DATA TEMPS		ARGUMENT AREA
	UNL
	IFEQ GENEVE
	LIST
	CLR  R1
	MOV  @AFDESC,R6		GET DESCRIPTOR
	MOV  @CSAVF$,R4		GET "MOVE TO" ADDRESS
	INC  R4
	CLR  R2
	CLR  R5
*
*  MOVE FILE ID TO PAB KEEPING COUNT OF # CHARS
*
MOVLOP	EQU  $
	CI   R2,40		OVERFLOWED FILE ID ?
	JEQ  CHAERR		BRIF YES, ERROR
	MOVB *R6+,R5		GET A CHARACTER
	CI   R5,>2000		IS IT A BLANK ?
	JEQ  MOVEND		BRIF YES, DONE
	INC  R2
	MOVB R5,*R4+
	JMP  MOVLOP
MOVEND	EQU  $
	SWPB R2
	MOV  @CSAVF$,R4		GET SAVE FILE ADDRESS AGAIN
	MOVB R2,*R4		SAVE LENGTH
	BL   @CCALP$		CALL PROGRAM
	JMP  MENERR		IF RETURN, MENU HAD AN ERROR
	UNL
	ELSE
	LIST
	LI   R0,PARFIL		PARSE FILE
	MOV  @AFDESC,R1		GET FIELD DESCRIPTOR START
	LI   R2,LOWRAM		POINTER TO OUTPUT STRING LENGTH
	LI   R3,32*256		SIZE OF STRING AREA
	MOVB R3,*R2
	CLR  R3
	XOP  @DUTLXP,0		DO IT
	MOV  R1,R1		ANY ERROR?
	JNE  CHAERR		BRIF YES, ERROR RETURN
*
*  FILE NAME IN LOW RAM AT >0410.  NOTE I PLACE IT HERE CAUSE OF A
*  TEMPORARY MDOS 1.14 BUG, THE STRING MUST NOW RESIDE BELOW >2000.
*  PAUL C. HAS PROMISED THIS WILL BE FIXED IN MDOS 1.15+ (WHATEVER
*  THAT IS).
*
	MOV  R2,R1		FILE NAME POINTER
	LI   R0,LOAPRO		LOAD PROGRAM IMAGE
	XOP  @DUTLXP,0		CALL PROGRAM
	MOV  R0,R1		ERROR CODE IN R1
	JMP  MENERR		RETURN
	ENDIF
	LIST
*
*  CHAERR IS CALLED WHEN FILE NAME PASSED IS TOO LONG (>40 BYTES)
*
CHAERR  EQU  $
	LI   R1,-1
	JMP  MENERR
*
*  EXIT SEQUENCE.  NOTE THAT THIS IS ONLY ENTERED IF SOME TYPE OF
*  ERROR HAS OCCURRED.
*
MENERR  EQU  $
	MOV  @AERROR,R5
	MOV  R1,*R5		SET ERROR RETURN
	MOV  @TEMPS,R3
	MOV  @TEMPS+2,R11
	B    *R11		RETURN
*
*  DATA AREA:
*
TEMPS	EQU  $
	BSS  4			DATA AREA SAVE
AFDESC	BSS  2			FILE IDENTIFICATION
AERROR	BSS  2			ERROR FLAG
	IF   GENEVE
DUTLXP	DATA UTLXOP		UTILITY XOP NUMBER
	ENDIF
	END
