	TITL 'II$ - INTEGER TO INTEGER EXPONENTATION'
	IDT  'II$'
*
*  This routine computes the exponent of an integer number with
*  and integer exponent.
*
*  It is called by the FORTRAN runtime when a construct of the type:
*
*		    base ** power
*
*  is used, where base and power are both integer arguments.
*
*  Revision History:
*
*   17-April-1987:  Revised to handle negative base arguments
*
	DEF  II$
*
*  REGISTER VARIABLES
*
BASE	EQU  0
ANS	EQU  2
NEGFLA	EQU  3
*
II$	EQU  $
	DATA -2	    		2 ARGUMENTS
	DATA TEMPS
	CLR  NEGFLA	    	! CLEAR THE NEGATE FLAG AT END
	MOV  @BASEAR,R6
	MOV  *R6,R5		LOAD BASE & POWER
	JEQ  II$3
	JGT  II$C	      	!
	MOV  @POWARG,NEGFLA   	! Get the power
	MOV  *NEGFLA,NEGFLA   	! 
	ANDI NEGFLA,1	  	! If its odd, then must negate at end
	ABS  R5		 	! Make the argument positive
II$C	EQU  $
	MOV  R5,BASE      	TEST FOR SPECIAL CASES
	MOV  @POWARG,R6
	MOV  *R6,R5
	JLT  II$4
	MOV  R5,R1
	LI   ANS,1	 	INITIALIZE ANSWER TO 1
II$1	EQU  $
	MOV  R1,R5
	JEQ  II$6	  	DONE IF POWER=0
	CLR  R4	    		IF NOT SHIFT R5 TO TEST
	SRL  R5,1	  	RIGHT-MOST BIT
	JNC  NOTBIT
	SETO R4	    		SET BIT IF CARRY
NOTBIT	EQU  $	     		(DOUBLE WORD SHIFT)
	MOV  R5,R1	 	SAVE SHIFTED R5
	MOV  R4,R4
	JEQ  II$2
	MOV  ANS,R4		MULTIPLY APPROPRIATE POWER OF BASE
	MPY  BASE,R4      	INTO ANS
	MOV  R4,R4	 	IF >0, OVERFLOW
	JNE  II$5
	MOV  R5,ANS		ELSE, SAVE RESULT
II$2	EQU  $	     		RAISE BASE TO NEXT POWER
	MOV  R1,R5
	JEQ  II$6
	MOV  BASE,R4      	1,2,4,8,... FOR NEXT
	MPY  BASE,R4      	BIT TO BE SHIFTED, RESTORE
	MOV  R4,R4
	JNE  II$5	  	BRIF BASE OVERFLOW
	MOV  R5,BASE
	JMP  II$1
II$3	EQU  $
	MOV  @POWARG,R6		IF BASE=0, THEN TEST POWER
	MOV  *R6,R5
	JLT  II$A
II$7	EQU  $
	CLR  R5	    		IF POWER .GE. 0 THEN ANS=0
	JMP  BRETUR
II$A	EQU  $	     		IF POWER .LT. 0 THEN ANS=
	LI   R5,>8001		NEGATIVE MAX
	JMP  BRETUR
II$9	EQU  $
	LI   R5,>8000
	JMP  BRETUR
II$4	EQU  $
	CLR  R4
	SRL  R5,1
	JNC  NOTBI1
	SETO R4
NOTBI1	EQU  $			IF POWER .LT. 0 THEN
	MOV  BASE,R5		TEST BASE
	CI   R5,1
	JEQ  BRETUR		IF BASE .EQ. 1, THEN ANS .EQ. 1
	CI   R5,-1	 	IF BASE .EQ. -1, THEN ANS=+1 (EVEN POWER)
	JNE  II$8
	MOV  R4,R4
	JLT  II$5	  	ELSE, OVERFLOW, RETURN 0
	LI   R5,1
	JMP  BRETUR
II$5	EQU  $	     		IF OVERFLOW, CHECK SIGN & RETURN + OR - MAX
	MOV  R4,R4
	JLT  II$9
	JMP  II$A
II$6	EQU  $	     		NORMAL RETURN
	MOV  ANS,R5
	JMP  BRETUR
II$8	EQU  $	     		ERROR RETURN
	JMP  II$7
BRETUR	EQU  $
	MOV  NEGFLA,NEGFLA
	JEQ  BRETU1		! Brif no need to negate
	NEG  R5	    		! Negate result
BRETU1	EQU  $
	MOV  @TEMPS,R3    	RESTORE BASE
	MOV  R5,R5	 	! Set status on result
	B    *R11	  	& RETURN
*
*  DATA AREA:
*
TEMPS	BSS  4
BASEAR	BSS  2	     		BASE
POWARG	BSS  2	     		POWER
	END
