	TITL 'MAGNIF - MAGNIFY SPRITE'
	IDT  'MAGNI'
*
*  THIS SUBROUTINE ALLOWS FOR SPRITE MAGNIFICATION
*
*  CALLING SEQUENCE:
*
*     CALL MAGNIF ( MAGNIFICATION FACTOR )
*
*  WHERE:
*
*    MAGNIFICATION - IS A NUMBER FROM 1 TO 4 AS FOLLOWS:
*    FACTOR
*		1.  NORMAL SIZE
*		2.  SINGLE SIZE & MAGNIFIED
*		3.  DOUBLE SIZE & UNMAGNIFIED
*		4.  DOUBLE SIZE & MAGNIFIED
*
*  Update History:
*
*    14-Feb-88   A.Beard   Set location >83D4 to same as VWTR.  Solves
*			   problem of magnification disappearing on key
*			   entry.
*
*    22-Nov-88   A.Beard   Updated for MDOS 1.14 compatible version.
*    04-JAN-90   V4.42     Cosmetic
*
*  Copyright 1990 by LGMA Products
*
*  DEFINITIONS:
*
	DEF  MAGNIF
*
	COPY "FORTPAR:"
*
	UNL
	COPY "EQUATES:VID.EQ9"
	COPY "EQUATES:FORTEX.EQ9"
	LIST
*
*  MAIN ENTRY:
*
MAGNIF	EQU  $
	DATA -1			1 ARGUMENT
	DATA TEMPS		TEMP DATA AREA
	UNL
	IF   GENEVE
	LIST
	LI   R0,GETVID		GET VIDEO MODE
	XOP  @DVIDXP,0
	CI   R0,TEXT1		TEXT 1 MODE?
	JEQ  SET40E
	CI   R0,TEXT2		TEXT 2 MODE?
	JEQ  SET40E
	ELSE
	LIST
	MOV  @SET40F,R5		40 COL MODE?
	CI   R5,TEXT1
	JEQ  SET40E		BRIF YES
	CI   R5,TEXT2
	JEQ  SET40E
	ENDIF
	MOV  @AMAGNI,R5
	JEQ  MAGERR
	MOV  *R5,R5
	JLT  MAGERR		MUST BE 1 TO 4
	JEQ  MAGERR
	CI   R5,4
	JGT  MAGERR
	UNL
	IF   GENEVE
	LIST
	MOV  R5,R1		DO MAGNIFY XOP
	LI   R0,SPRMAG		SPRITE MAGNIFY
	XOP  @DVIDXP,0		DO IT
	ELSE
	LIST
	DEC  R5			0 TO 3
	SWPB R5			PUT IN HIGH BYTE
	MOVB @REFSCR,R0		GET CURRENT VALUE
	ANDI R0,>FC00		MASK CURRENT SETTING
	A    R5,R0		SET VALUE OF SPRITE MAGNIFY
	MOVB R0,@REFSCR
	SWPB R0
	AI   R0,>0100		SET VDP REGISTER 1
	BLWP @CVWTR$
	ENDIF
	JMP  MAGRET
*
SET40E	EQU  $
	BLWP *R10
	DATA ERROR
	TEXT 'S4'		40 COL SCREEN ERROR
	JMP  MAGRET
*
MAGERR	EQU  $
	BLWP *R10		MAGNIFICATION FACTOR ERROR (1-4)
	DATA ERROR
	TEXT 'MF'
*
MAGRET EQU  $
	MOV  @TEMPS,R3
	B    *R11		RETURN
*
*  DATA AREA
*
TEMPS  BSS  4
AMAGNI BSS  2			MAGNIFICATION FACTOR (1 TO 4 )
*
	UNL
	IF   GENEVE
	LIST
DVIDXP	DATA VIDXOP		VIDEO XOP NUMBER
	ENDIF
	LIST
	END
