	TITL 'GET VIDEO MODE SUBROUTINE'
	IDT  'GETMOD'
*
*  GETMOD : GET VIDEO MODE  (MDOS ONLY)
*
*  THIS ROUTINE IS CALLED AS FOLLOWS:
*
*	CALL GETMOD ( MODE [, COLUMNS, ROWS])
*
*  WHERE:
*
*	MODE    :  RETURNS THE VIDEO MODE, 0 TO 7
*	COLUMNS :  IS THE CURRENT NUMBER OF COLUMNS
*	ROWS    :  IS THE CURRENT NUMBER OF ROWS
*
*  COPYRIGHT 1989 BY LGMA PRODUCTS
*
*  UPDATE HISTORY:
*
*    VERSION 4.1   02-OCT-88    MDOS VERSION
*                  13-NOV-88    WORKING MDOS 1.14 VERSION (WAS RETURNING
*				WRONG REGISTERS).
*    VERSION 4.4   28-AUG-89    TI-99 VERSION
*            4.41  02-DEC-89    USE VRAM TABLE
*
	DEF  GETMOD
*
	COPY "FORTPAR:"
	UNL
	COPY "EQUATES:VID.EQ9"
	COPY "EQUATES:FORTEX.EQ9"
	LIST
*
GETMOD	EQU  $
	DATA -3			3 ARGUMENTS
	DATA TEMPS
*
*  GENEVE VERSION
*
	IF   GENEVE
	LI   R0,GETVID
	XOP  @DVIDXP,0		GET THE MODE
	MOV  @AMODE,R5
	MOV  R0,*R5
	MOV  @ACOL,R5		GET # COLUMNS
	JEQ  GETRET
	MOV  R1,*R5
	MOV  @AROW,R5		GET # ROWS
	JEQ  GETRET
	MOV  R2,*R5
*
*  TI-99 VERSION
*
	ELSE
	MOV  @AMODE,R5
	MOV  @SET40F,*R5	RETURNED MODE
*
	MOV  @ACOL,R5		GET NUMBER OF COLUMNS
	JEQ  GETRET
*
	MOV  @TABADD,R6
	MOV  *R6,*R5		SET # OF COLUMNS
*
	MOV  @AROW,R5		GET NUMBER OF ROWS
	JEQ  GETRET
	LI   R6,24		ALWAYS IS 24 COLUMNS
	MOV  R6,*R5
	ENDIF
*
GETRET	EQU  $
	MOV  @TEMPS,R3		RESTORE BASE
	B    *R11		AND RETURN
*
	IF   GENEVE
DVIDXP	DATA VIDXOP		VIDEO XOP NUMBER
	ENDIF
*
TEMPS	BSS  4
AMODE	BSS  2			MODE NUMBER
ACOL	BSS  2			COLUMNS
AROW	BSS  2			ROWS
*
	END
