	TITL 'BLOCK SCROLL WINDOW UP/DOWN/LEFT/RIGHT'
	IDT  'BSCRWI'
*
*     BLKSUP   :    SCROLL WINDOW UP
*     BLKSDN   :    SCROLL WINDOW DOWN
*     BLKSLE   :    SCROLL WINDOW LEFT
*     BLKSRI   :    SCROLL WINDOW RIGHT
*
*  CALLING SEQUENCE:
*
*	CALL BLKSUP ( NOPIXELS, IROWUL, ICOLUL, IROWLR, IRCOLLR,
*                     IPIXCO )
*	CALL BLKSDN ( NOPIXELS, IROWUL, ICOLUL, IROWLR, IRCOLLR,
*                     IPIXCO )
*	CALL BLKSLF ( NOPIXELS, IROWUL, ICOLUL, IROWLR, IRCOLLR,
*                     IPIXCO )
*	CALL BLKSRI ( NOPIXELS, IROWUL, ICOLUL, IROWLR, IRCOLLR,
*                     IPIXCO )
*
*  WHERE:
*
*       NOPIXELS :  IS THE NUMBER OF LINES TO SCROLL
*       IROWUL   :  IS THE ROW NUMBER OF UPPER LEFT CORNER
*       ICOLUL   :  IS THE COLUMN NUMBER OF UPPER LEFT CORNER
*       IROWLR   :  IS THE ROW NUMBER OF LOWER LEFT CORNER
*       ICOLLR   :  IS THE COLUMN NUMBER OF LOWER LEFT CORNER
*       IPIXCO   :  IS THE PIXEL COLOR FOR BLANK PIXELS
*
*  COPYRIGHT 1988 BY LGMA PRODUCTS
*
*  UPDATE HISTORY:
*
*    VERSION 4.1   02-OCT-88    MDOS VERSION
*
	DEF  BLKSUP
	DEF  BLKSDN
	DEF  BLKSLE
	DEF  BLKSRI
*
	COPY "FORTPAR:"
*
	UNL
	COPY "EQUATES:VID.EQ9"
	LIST
*
BLKSUP	EQU  $
	DATA -6			6 ARGUMENTS
	DATA TEMPS
	LI   R0,BSCRUP		SCROLL UP
	JMP  SCRCOM
*
BLKSDN	EQU  $
	DATA -6
	DATA TEMPS
	LI   R0,BSCRDN
	JMP  SCRCOM
*
BLKSLE	EQU  $
	DATA -6
	DATA TEMPS
	LI   R0,BSCRLE
	JMP  SCRCOM
*
BLKSRI	EQU  $
	DATA -6
	DATA TEMPS
	LI   R0,BSCRRI
*
SCRCOM	EQU  $
	LI   R8,TEMPS+4		MOVE EIGHT ARGUMENTS
	MOV  *R8+,R1
	MOV  *R1,R1		GET NUMBER PIXELS
	MOV  *R8+,R2
	MOV  *R2,R2		GET ROW NUMBER
	MOV  *R8+,R3
	MOV  *R3,R3		GET COLUMN NUMBER
	MOV  *R8+,R4
	MOV  *R4,R4		GET ROW NUMBER
	MOV  *R8+,R5
	MOV  *R5,R5		GET COLUMN NUMBER
	MOV  *R8+,R6
	MOV  *R6,R6		GET PIXEL COLOR
	SLA  R6,8		IN HIGH BYTE
	XOP  @DVIDXP,0		DO IT
	MOV  @TEMPS,R3
	B    *R11		RETURN
*
DVIDXP	DATA VIDXOP		VIDEO XOP NUMBER
*
TEMPS	BSS  4
	BSS  12			ROOM FOR 6 ARGUMENTS
	END
