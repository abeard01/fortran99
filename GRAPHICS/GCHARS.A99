	TITL 'GCHAR - GET CHARACTER FROM SCREEN'
	IDT  'GCHAR'
*
*  THIS ROUTINE IS CALLED TO GET A CHARACTER FROM THE SCREEN.
*  THE MDOS VERSION OPTIONALLY RETURNS THE COLOR INFORMATION.
*
*  CALLING SEQUENCE:
*
*      CALL GCHAR ( ROW, COLUMN, CHARACTER [,FORE COLOR, BACK COLOR] )
*
*  WHERE:
*
*  	ROW	   -  IS THE ROW NUMBER (1 TO 24)
*
*  	COLUMN     -  IS THE COLUMN NUMBER (1 TO 32 OR 1 TO 40)
*
*  	CHARACTER  -  IS THE ASCII CHARACTER AT THAT SCREEN LOCATION.
*
*  	FORE COLOR -  IS THE FOREGROUND COLOR OF THE CHARACTER (OPTIONAL, MDOS
*		      ONLY)
*
*  	BACK COLOR -  IS THE BACKGROUND COLOR OF THE CHARACTER (OPTIONAL, MDOS
*		      ONLY)
*
*  COPYRIGHT 1988 BY LGMA PRODUCTS
*
*  UPDATE HISTORY:
*
*    19-SEP-88   VERSION 4.0     MDOS VERSION WITH ADDITIONAL ARGUMENTS
*
*  DEFINITIONS:
*
	DEF  GCHAR
*
	COPY "FORTPAR:"
	UNL
	COPY "EQUATES:FORTEX.EQ9"
	COPY "EQUATES:VID.EQ9"
	LIST
*
*  MAIN ENTRY:
*
GCHAR	EQU  $
	IFEQ GENEVE
	DATA -3			3 ARGUMENTS
	DATA TEMPS
	MOV  @SET40F,R5		GET 40 COL. MODE FLAG
	CI   R5,TEXT1		IS THIS 40 COL MODE?
	JEQ  SET40
	CI   R5,TEXT2
	JEQ  SET80
	LI   R5,32		ELSE, 32 COLUMN MODE
	JMP  SETCON
SET40	EQU  $
	LI   R5,40
	JMP  SETCON
SET80	EQU  $
	LI   R5,80
SETCON	EQU  $
	MOV  @AROW,R0		GET ROW
	MOV  *R0,R0
	JLT  BADROW		BRIF BAD
	JEQ  BADROW
	CI   R0,24		ROW >24?
	JGT  BADROW		BRIF YES
	DEC  R0			ZERO OFFSET
	MPY  R5,R0		* 32 OR * 40
	MOV  R1,R0
	MOV  @ACOL,R6		 + COLUMN
	MOV  *R6,R1
	JLT  BADCOL
	JEQ  BADCOL
	C    R1,R5		< MAX FOR COLUMN
	JGT  BADCOL
	A    *R6,R0
	DEC  R0			- 1 FOR FORTRAN OFFSET
	CLR  R1
	BLWP @CVSBR$		READ THE CHARACTER
	SWPB R1
	MOV  @ACHAR,R6
	MOV  R1,*R6		AND MOVE
*
*  GENEVE VERSION OF THE GCHAR ROUTINE
*
	ELSE
	DATA -5
	DATA TEMPS		5 POSSIBLE ARGUMENTS
	LI   R0,GETVID		GET VIDEO MODE
	XOP  @DVIDXP,0
	MOV  R1,R7		SAVE # OF COLUMNS
	MOV  R2,R6		SAVE # OF ROWS
	MOV  @AROW,R0		GET ROW NUMBER
	JEQ  BADROW
	MOV  *R0,R0
	JLT  BADROW
	JEQ  BADROW
	C    R0,R6		MAX OUT ON ROWS?
	JGT  BADROW		ROW NUMBER MUST BE 1 TO 24
	MOV  @ACOL,R1
	JEQ  BADCOL
	MOV  *R1,R1
	JLT  BADCOL
	JEQ  BADCOL
	C    R1,R7		MAX OUT ON COLUMNS?
	JGT  BADCOL
	MOV  R1,R2		GET REGISTERS IN PROPER PLACE
	MOV  R0,R1
	LI   R0,GETCCO		GET CHARACTER AND COLOR
	DEC  R1			DECREMENT ROW AND COLUMN
	DEC  R2
	XOP  @DVIDXP,0		DO VIDEO XOP
	MOV  @ACHAR,R5		STORE IN CHARACTER
	MOV  R0,*R5
	MOV  @AFORE,R5		FOREGROUND SPECIFIED?
	JEQ  CHARRE		BRIF NO, EXIT
	MOV  R1,R2		FORM FOREGROUND COLOR
	SRA  R2,8
	ANDI R2,>FF
	MOV  R2,*R5
	MOV  @ABACK,R5		BACKGROUND SPECIFIED?
	JEQ  CHARRE		BRIF NO, EXIT
	ANDI R1,>FF		FORM BACKGROUND COLOR
	MOV  R1,*R5
	ENDIF
	JMP  CHARRE
BADROW	EQU  $
	MOV  R0,R5		FOR DISPLAY
	BLWP *R10
	DATA ERROR
	TEXT 'RO'		BAD ROW # (1-24)
	JMP  CHARRE
BADCOL	EQU  $
	MOV  R1,R5
	BLWP *R10
	DATA ERROR
	TEXT 'CO'		BAD COLUMN # (1-32, OR 1-40)
CHARRE	EQU  $
	MOV  @TEMPS,R3		RESTORE R3
	B    *R11		WHEN DONE, RETURN
*
*  DATA AREA
*
TEMPS	BSS  4			SAVE AREA
AROW	BSS  2			ROW
ACOL	BSS  2			COLUMN
ACHAR	BSS  2			CHARACTER
	IF   GENEVE
AFORE	BSS  2			FOREGROUND COLOR (OPTIONAL)
ABACK	BSS  2			BACKGROUND COLOR (OPTIONAL)
DVIDXP	DATA VIDXOP
	ENDIF
	END
