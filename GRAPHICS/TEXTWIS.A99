	TITL 'SET/GET TEXT WINDOW'
	IDT  'SETWIN'
*
*  SETTWN   :    SET TEXT WINDOW    ( MDOS ONLY )
*  GETTWN   :    GET TEXT WINDOW    ( MDOS ONLY )
*
*  CALLING SEQUENCE:
*
*       CALL SETTWN ( ITOP, ILEFT, IBOTTOM, IRIGHT )
*       CALL GETTWN ( ITOP, ILEFT, IBOTTOM, IRIGHT )
*
*  WHERE:
*
*	ITOP    :  IS THE TOP ROW NUMBER
*	ILEFT   :  IS THE LEFT ROW NUMBER
*	IBOTTOM :  IS THE BOTTOM ROW NUMBER
*	IRIGHT  :  IS THE RIGHT ROW NUMBER
*
*  COPYRIGHT 1988 BY LGMA PRODUCTS
*
*  UPDATE HISTORY:
*
*    VERSION 4.1   02-OCT-88    MDOS VERSION
*                  21-NOV-88    FIXED BUG IN GETTWN, NOT RETURNING RIGHT.
*
	DEF  SETTWN
	DEF  GETTWN
*
	COPY "FORTPAR:"
	UNL
	COPY "EQUATES:VID.EQ9"
	LIST
*
SETTWN	EQU  $
	DATA -4			4 ARGUMENTS
	DATA TEMPS
	LI   R5,TEMPS+4		MOVE FROM ADDRESS
	STWP R6			MOVE TO ADDRESS
	INCT R6
	LI   R7,4		4 WORDS TO MOVE
*
SETLOP	EQU  $
	MOV  *R5+,R8		GET NEXT ADDRESS TO MOVE FROM
	MOV  *R8,*R6+		MOVE TO REGISTER
	DEC  R7
	JNE  SETLOP
*
	LI   R0,SETTWI		SET TEXT WINDOW
	XOP  @DVIDXP,0		DO IT
	JMP  SETRET
*
GETTWN	EQU  $
	DATA -4			4 ARGUMENTS
	DATA TEMPS
	LI   R0,GETTWI		GET TEXT WINDOW
	XOP  @DVIDXP,0		DO IT
	STWP R4			MOVE FROM ADDRESS
	LI   R5,TEMPS+4		MOVE TO ADDRESS
	LI   R6,4		# WORDS TO MOVE
MOVLOP	EQU  $
	MOV  *R5+,R7		GET NEXT ADDRESS TO STORE
	MOV  *R4+,*R7		STORE NEXT VALUE
	DEC  R6
	JNE  MOVLOP
SETRET	EQU  $
	MOV  @TEMPS,R3		RESTORE BASE
	B    *R11		RETURN
*
DVIDXP	DATA VIDXOP		VIDEO XOP NUMBER
*
TEMPS	BSS  4
ATOP	BSS  2			TOP
ALEF	BSS  2			LEFT
ARIG	BSS  2			RIGHT
ABOT	BSS  2			BOTTOM
	END
