	TITL 'POSITI - RETURN SPRITE POSITION'
	IDT  'POSITI'
*
*
*  THIS SUBROUTINE ALLOWS CREATION OF SPRITES
*
*  CALLING SEQUENCE:
*
*  	CALL POSITI ( SPRITE NUMBER, DOT-ROW, DOT-COLUMN )
*
*  WHERE:
*
*  	SPRITE    -  IS THE NUMBER OF THE SPRITE.  IT RANGES FROM
*  	NUMBER		1 TO 32.
*
*  	DOT	  -  IS THE RETURNED ROW # FROM 1 TO 255
*  	ROW
*
*  	DOT	  -  IS THE RETURNED COLUMN # FROM 1 TO 255
*  	COLUMN
*
*  Update History:
*
*      ?????       Version 3.1     Initial Release
*
*      22-Nov-88   Version 4.1     Updated for MDOS compatible version.
*      04-Jan-90   Version 4.41    Cosmetic
*
*  DEFINITIONS:
*
	DEF  POSITI
*
*  EQUATES:
*
	COPY "FORTPAR:"
	UNL
	COPY "EQUATES:VID.EQ9"
	COPY "EQUATES:FORTEX.EQ9"
	LIST
*
*  MAIN ENTRY:
*
POSITI	EQU  $
	DATA -3			3 ARGUMENTS
	DATA TEMPS		TEMP DATA AREA
	UNL
	IF   GENEVE
	LIST
	LI   R0,GETVID		GET VIDEO MODE
	XOP  @DVIDXP,0
	CI   R0,TEXT1		MAKE SURE NOT TEXT1 OR TEXT2 MODE
	JEQ  SET40E
	CI   R1,TEXT2
	JEQ  SET40E
	ELSE
	LIST
	MOV  @SET40F,R5   	40 COLUMN MODE?
	CI   R5,TEXT1
	JEQ  SET40E		BRIF 40 COLUMN MODE, ERROR
	CI   R5,TEXT2
	JEQ  SET40E		BRIF 80 COLUMN MODE, ERROR
	LI   R0,SPRVDP    	SPRITE ATTRIBUTE LIST
	ENDIF
	MOV  @ANUMB,R5    	SPRITE NUMBER
	JEQ  SPRERR
	MOV  *R5,R5
	JLT  SPRERR		BRIF <0, ERROR
	CI   R5,32
	JGT  SPRERR
	UNL
	IF   GENEVE
	LIST
	MOV  R5,R1		SET IT
	LI   R0,SPRPOS		GET SPRITE POSITION
	XOP  @DVIDXP,0
	MOV  @ADOTR,R5
	JEQ  POSRET
	MOV  R0,*R5
	MOV  @ADOTC,R5
	JEQ  POSRET
	MOV  R1,*R5
	JMP  POSRET
	ELSE
	LIST
	DEC  R5
	SLA  R5,2		*4
	A    R5,R0		ADDRESS OF ATTRIBUTE TABLE
	CLR  R1
	BLWP @CVSBR$		GET DOT-ROW
	INC  R0
	MOV  R1,R4
	BLWP @CVSBR$		GET DOT-COLUMN
	MOV  R1,R5
	SWPB R4
	SWPB R5
	MOV  @ADOTR,R1
	MOV  R4,*R1		SAVE DOT-ROW
	MOV  @ADOTC,R1
	MOV  R5,*R1		SAVE DOT-COLUMN
	ENDIF
	JMP  POSRET
*
SET40E	EQU  $
	BLWP *R10
	DATA ERROR
	TEXT 'S4'		40 COLUMN MODE ERROR
	JMP  POSRET
*
SPRERR	EQU  $
	BLWP *R10
	DATA ERROR
	TEXT 'SR'		INVALID SPRITE # (1 TO 32)
*
POSRET	EQU  $
	MOV  @TEMPS,R3
	B    *R11		RETURN
*
*  DATA AREA
*
TEMPS	BSS  4
ANUMB	BSS  2	  		SPRITE NUMBER ( 1 TO 32 )
ADOTR	BSS  2	  		DOT-ROW POSITION
ADOTC	BSS  2	  		DOT-COLUMN POSITION
*
	UNL
	IF   GENEVE
	LIST
DVIDXP	DATA VIDXOP
	ENDIF
	LIST
	END
