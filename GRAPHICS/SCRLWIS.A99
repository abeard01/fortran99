	TITL 'SCROLL (MOVE) WINDOW UP/DOWN/LEFT/RIGHT'
	IDT  'SCRWIN'
*
*     SCRLUP   :    SCROLL WINDOW UP
*     SCRLDN   :    SCROLL WINDOW DOWN
*     SCRLLE   :    SCROLL WINDOW LEFT
*     SCRLRI   :    SCROLL WINDOW RIGHT
*
*  CALLING SEQUENCE:
*
*	CALL SCRLUP ( NOLINES, IROWUL, ICOLUL, IROWLR, IRCOLLR,
*                     ICHAR, IFORE, IBACK )
*	CALL SCRLDN ( NOLINES, IROWUL, ICOLUL, IROWLR, IRCOLLR,
*                     ICHAR, IFORE, IBACK )
*	CALL SCRLLF ( NOLINES, IROWUL, ICOLUL, IROWLR, IRCOLLR,
*                     ICHAR, IFORE, IBACK )
*	CALL SCRLRI ( NOLINES, IROWUL, ICOLUL, IROWLR, IRCOLLR,
*                     ICHAR, IFORE, IBACK )
*
*  WHERE:
*
*       NOLINES :  IS THE NUMBER OF LINES TO SCROLL
*       IROWUL  :  IS THE ROW NUMBER OF UPPER LEFT CORNER
*       ICOLUL  :  IS THE COLUMN NUMBER OF UPPER LEFT CORNER
*       IROWLR  :  IS THE ROW NUMBER OF LOWER LEFT CORNER
*       ICOLLR  :  IS THE COLUMN NUMBER OF LOWER LEFT CORNER
*       ICHAR   :  IS THE CHARACTER TO BLANK FILL
*       IFORE   :  IS THE FOREGROUND COLOR NUMBER
*       IBACK   :  IS THE BACKGROUND COLOR NUMBER
*
*  COPYRIGHT 1988 BY LGMA PRODUCTS
*
*  UPDATE HISTORY:
*
*    VERSION 4.1   02-OCT-88    MDOS VERSION
*
	DEF  SCRLUP
	DEF  SCRLDN
	DEF  SCRLLE
	DEF  SCRLRI
*
	COPY "FORTPAR:"
	UNL
	COPY "EQUATES:VID.EQ9"
	LIST
*
SCRLUP	EQU  $
	DATA -8			8 ARGUMENTS
	DATA TEMPS
	LI   R0,SCRWUP		SCROLL UP
	JMP  SCRCOM
*
SCRLDN	EQU  $
	DATA -8
	DATA TEMPS
	LI   R0,SCRWDN
	JMP  SCRCOM
*
SCRLLE	EQU  $
	DATA -8
	DATA TEMPS
	LI   R0,SCRWLE
	JMP  SCRCOM
*
SCRLRI	EQU  $
	DATA -8
	DATA TEMPS
	LI   R0,SCRWRI
*
SCRCOM	EQU  $
	LI   R8,TEMPS+4		MOVE EIGHT ARGUMENTS
	MOV  *R8+,R1
	MOV  *R1,R1		GET NUMBER LINES
	MOV  *R8+,R2
	MOV  *R2,R2		GET ROW NUMBER
	MOV  *R8+,R3
	MOV  *R3,R3		GET COLUMN NUMBER
	MOV  *R8+,R4
	MOV  *R4,R4		GET ROW NUMBER
	MOV  *R8+,R5
	MOV  *R5,R5		GET COLUMN NUMBER
	MOV  *R8+,R6
	MOV  *R6,R6		GET CHARACTER VALUE FOR BLANK LINES
	ANDI R6,>FF		MASK ASCII CHARACTER (8-BIT)
	MOV  *R8+,R7
	MOV  *R7,R7		GET BACKGROUND COLOR FOR BLANK LINES
	SLA  R7,8
	MOV  *R8+,R8
	MOV  *R8,R8		GET FOREGROUND COLOR FOR BLANK LINES
	ANDI R8,>FF
	SOC  R8,R7
	XOP  @DVIDXP,0		DO IT
	MOV  @TEMPS,R3
	B    *R11		RETURN
*
DVIDXP	DATA VIDXOP		VIDEO XOP NUMBER
*
TEMPS	BSS  4
	BSS  16			ROOM FOR 8 ARGUMENTS
	END
