	TITL 'MOTION - DEFINE SPRITE MOTION'
	IDT  'MOTION'
*
*  THIS SUBROUTINE ALLOWS DEFINITION OF MOTION OF A SPRITE.
*
*  CALLING SEQUENCE:
*
*  	CALL MOTION ( SPRITE NUMBER, ROW VELOCITY, COLUMN VELOCITY )
*
*  WHERE:
*
*  	SPRITE	-  IS THE NUMBER OF THE SPRITE.  IT RANGES FROM
*  	NUMBER		1 TO 32.
*
*  	ROW	-  IS THE SPEED OF THE Y DIRECTION, FROM 0 TO FF
*  	VELOCITY
*
*  	COLUMN	-  IS THE SPEED OF THE X DIRECTION, FROM 0 TO FF
*  	VELOCITY
*
*  Change History:
*
*  v3.0   20-June-87:   Changed address of INTFLG.
*
*  v3.1   31-Oct-87:    Made version compatible between v2.1 and v3.0.
*		   	Fixes applied per Kerry Barlow.  Major problem
*			with old version was that it wrote into the "system"
*			bytes, which on the surface looked like an ok thing
*			to do, but was discovered to cause problems.
*
*  v4.1   22-Nov-88:    MDOS 1.14 Compatible Version
*
*  v4.4   04-Jan-90:    Cosmetic.
*
*  Copyright 1988 by LGMA Products
*
*  DEFINITIONS:
*
	DEF  MOTION
*
*  EQUATES:
*
	COPY "FORTPAR:"
	UNL
	COPY "EQUATES:VID.EQ9"
	COPY "EQUATES:FORTEX.EQ9"
	LIST
*
	UNL
	IF   GENEVE
	LIST
SPRNUM	EQU  IOBBUF		SPRITE ATTRIBUTE LIST IN HIGH RAM
	ENDIF
	LIST
*
*  MAIN ENTRY:
*
MOTION  EQU  $
	DATA -3			3 ARGUMENTS
	DATA BASADR     	TEMP DATA AREA
	UNL
	IF   GENEVE
	LIST
	LI   R0,GETVID		CHECK VIDEO MODE
	XOP  @DVIDXP,0
	CI   R0,TEXT1		MAKE SURE NOT TEXT1 OR TEXT2
	JEQ  SET40E
	CI   R0,TEXT2
	JEQ  SET40E
	ELSE
	LIST
	MOV  @SET40F,R5 	40 COLUMN MODE?
	CI   R5,TEXT1		IS THIS 40 COLUMN MODE?
	JEQ  SET40E		BRIF YES, ERROR
	CI   R5,TEXT2
	JEQ  SET40E		BRIF 80 COLUMN MODE, ERROR
	ENDIF
	MOV  @ANUMB,R5  	GET SPRITE NUMBER
	JEQ  SPRERR     	BRIF ARGUMENT MISSING
	MOV  *R5,R5
	JLT  SPRERR
	JEQ  SPRERR
	CI   R5,31      	VALID SPRITE NUMBER?
	JGT  SPRERR     	BRIF NO
	UNL
	IF   GENEVE
	LIST
	LI   R6,SPRNUM		SPRITE MOTION DATA
	MOV  R5,*R6+		SET SPRITE NUMBER
	ELSE
	LIST
	MOV  R5,R0      	DETERMINE MOTION LIST OFFSET
	DEC  R0
	SLA  R0,2		*4 (BYTES PER SPRITE)
	AI   R0,SPMOTI   	+ START OF SPRITE MOTION TABLE
	INC  R5			MAKE SURE INTERRUPT KNOWS HOW MANY SPRITES
	SWPB R5			 IN MOTION
	CB   R5,@NUMSPR
	JLT  NOMOTI     	OK
	MOVB R5,@NUMSPR 	ELSE, SET THEM IN MOTION
NOMOTI  EQU  $
	ENDIF
	LIST
	MOV  @AROWV,R1  	GET SPECIFIED ROW VELOCITY
	JEQ  RVERR      	BRIF ARGUMENT NOT SPECIFIED
	MOV  *R1,R1
	CI   R1,-128
	JLT  RVERR      	ROW VELOCITY -128 TO 127
	CI   R1,127
	JGT  RVERR
	UNL
	IF   GENEVE
	LIST
	MOV  R1,*R6+		SET ROW VELOCITY
	ELSE
	LIST
	SWPB R1
	BLWP @CVSBW$      	WRITE IT
	INC  R0
	ENDIF
	MOV  @ACOLV,R1
	JEQ  CVERR
	MOV  *R1,R1
	CI   R1,-128    	COLUMN VELOCITY -128 TO 127
	JLT  CVERR
	CI   R1,127
	JGT  CVERR
	UNL
	IF   GENEVE
	LIST
	MOV  R1,*R6+		SET COLUMN VELOCITY IN LIST
	LI   R0,SPRMOT		SET SPRITE MOTION
	LI   R1,SPRNUM		ATTRIBUTE LIST
	LI   R2,1		# OF SPRITES TO SET IN MOTION
	XOP  @DVIDXP,0
	ELSE
	LIST
	SWPB R1
	BLWP @CVSBW$
	ENDIF
	JMP  MOTRET
*
RVERR   EQU  $
	BLWP *R10
	DATA ERROR
	TEXT 'RV'		ROW VELOCITY ERROR (0-255)
	JMP  MOTRET
*
CVERR   EQU  $
	BLWP *R10
	DATA ERROR
	TEXT 'CV'		COLUMN VELOCITY ERROR (0-255)
	JMP  MOTRET
*
SET40E  EQU  $
	BLWP *R10
	DATA ERROR
	TEXT 'S4'		40 COLUMN MODE ERROR
	JMP  MOTRET
*
SPRERR  EQU  $
	BLWP *R10
	DATA ERROR
	TEXT 'SR'		SPRITE # ERROR
*
MOTRET  EQU  $
	LIMI 2			ENSURE INTERRUPTS ENABLED
	MOV  @BASADR,R3
	B    *R11
*
*  DATA AREA
*
BASADR  BSS  4
ANUMB   BSS  2			SPRITE NUMBER ( 1 TO 32 )
AROWV   BSS  2			ROW VELOCITY
ACOLV   BSS  2			COLUMN VELOCITY
*
	UNL
	IF   GENEVE
	LIST
DVIDXP	DATA VIDXOP		VIDEO XOP NUMBER
	ENDIF
	LIST
	END
