	TITL 'MOUSE RELATED SUBROUTINES'
	IDT  'MOUSE'
*
*  MOUSE :   SUBROUTINES WHICH HANDLE MOUSE LOCATIONS AND MOVEMENTS
*
*  CALLING SEQUENCES:
*
*	CALL SETMSE ( IXPOS, IYPOS, ISCALE )
*	CALL GETMSE ( IXPOS, IYPOS, ILEFT, IMID, IRIGHT )
*	CALL GETMSR ( IXPOS, IYPOS, ILEFT, IMID, IRIGHT )
*
*  WHERE:
*
*       IXPOS   :  IS THE X-COORDINATE OF THE MOUSE
*       IYPOS   :  IS THE Y-COORDINATE OF THE MOUSE
*       ISCALE  :  IS THE SCALE FACTOR FOR MOUSE SPEED (0 TO 7, 0=FAST)
*       ILEFT   :  IS .TRUE. IF LEFT BUTTON PRESSED
*	IMID    :  IS .TRUE. IF MIDDLE BUTTON PRESSED
*       IRIGHT  :  IS .TRUE. IF RIGHT BUTTON PRESSED
*
*  COPYRIGHT 1988 BY LGMA PRODUCTS
*
*  UPDATE HISTORY:
*
*    VERSION 4.1   02-OCT-88    MDOS VERSION
*
	DEF  GETMSE		GET MOUSE COORDINATES
	DEF  GETMSR		GET MOUSE RELATIVE
	DEF  SETMSE		SET MOUSE COORDINATES
*
	COPY "FORTPAR:"
	UNL
	COPY "EQUATES:VID.EQ9"
	LIST
*
SETMSE	EQU  $			SET MOUSE COORDINATES
	DATA -3			3 ARGUMENTS
	DATA TEMPS
	LI   R8,TEMPS+4		STUFF ARGUMENTS
	MOV  *R8+,R1
	MOV  *R1,R1		X POSITION
	MOV  *R8+,R2
	MOV  *R2,R2		Y POSITION
	MOV  *R8+,R3
	MOV  *R3,R3		SCALE FACTOR
	ANDI R3,>7		MAKE SURE 0-7
	LI   R0,SETMOU
	XOP  @DVIDXP,0		DO IT
	JMP  COMRTN
*
GETMSR	EQU  $
	DATA -5			5 ARGUMENTS
	DATA TEMPS
	LI   R0,GETMOR		GET MOUSE RELATIVE
	JMP  GETCOM
*
GETMSE	EQU  $
	DATA -5			5 ARGUMENTS
	DATA TEMPS
	LI   R0,GETMOU		GET MOUSE INFORMATION
GETCOM	EQU  $
	XOP  @DVIDXP,0		DO IT
	LI   R8,TEMPS+4		STUFF RESULTS
	MOV  *R8+,R4
	MOV  R1,*R4		X POSITION
	MOV  *R8+,R4
	MOV  R2,*R4		Y POSITION
	MOV  *R8+,R4		LEFT BUTTON
	MOV  *R8+,R5		MIDDLE BUTTON
	MOV  *R8,R6		RIGHT BUTTON
	SETO *R4		ASSUME ON TIL NOT
	SETO *R5
	SETO *R6
	MOV  R3,R3		GET BUTTON STATUS
	JLT  CHKMID		LEFT BUTTON ON
	CLR  *R4		ELSE, TURN LEFT BUTTON OFF
CHKMID	EQU  $
	SLA  R3,1
	JLT  CHKRIG		MIDDLE BUTTON ON
	CLR  *R5		ELSE, TURN MIDDLE BUTTON OFF
CHKRIG	EQU  $
	SLA  R3,1
	JLT  COMRTN		RIGHT BUTTON ON
	CLR  *R6		ELSE, TURN RIGHT BUTTON OFF
*
COMRTN	EQU  $
	MOV  @TEMPS,R3		RESTORE BASE
	B    *R11		AND RETURN
*
DVIDXP	DATA VIDXOP		VIDEO XOP NUMBER
*
TEMPS	BSS  4
	BSS  10			ROOM FOR 5 ARGUMENTS
	END
