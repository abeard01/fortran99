	TITL 'DELSPR - DELETE SPRITE DEFINITION'
	IDT  'DELSPR'
*
*  THIS SUBROUTINE ALLOWS DELETION OF THE SPECIFIED SPRITE.
*
*  CALLING SEQUENCE:
*
*  	CALL DELSPR ( SPRITE NUMBER )
*
*  WHERE:
*
*  	SPRITE    -  IS THE NUMBER OF THE SPRITE.  IT RANGES FROM
*  	NUMBER		1 TO 32.
*
*  Change History:
*
*   v3.0  20-June-87:   Changed address of INTFLG.
*   V4.1  22-Nov-88:    MDOS 1.14 Compatible Version, removed INTFLG
*   v4.41 04-Jan-90:    Cosmetic
*
*  DEFINITIONS:
*
	DEF  DELSPR
*
*  EQUATES:
*
	COPY "FORTPAR:"
	UNL
	COPY "EQUATES:VID.EQ9"
	COPY "EQUATES:FORTEX.EQ9"
	LIST
*
	UNL
	IF   GENEVE
	LIST
SPRNUM	EQU  IOBBUF		HIGH RAM AREA TO STUFF SPRITE LIST
	ENDIF
	LIST
*
*  MAIN ENTRY:
*
DELSPR	EQU  $
	DATA -1			1 ARGUMENT
	DATA TEMPS		TEMP DATA AREA
	UNL
	IF   GENEVE
	LIST
	LI   R0,GETVID		GET VIDEO MODE
	XOP  @DVIDXP,0
	CI   R0,TEXT1		MAKE SURE NOT TEXT MODE
	JEQ  SET40E
	CI   R0,TEXT2
	JEQ  SET40E
	ELSE
	LIST
	MOV  @SET40F,R5   	40 COLUMN MODE?
	CI   R5,TEXT1
	JEQ  SET40E		BRIF YES, ERROR
	CI   R5,TEXT2
	JEQ  SET40E
	ENDIF
	MOV  @ANUMB,R5    	GET SPRITE NUMBER TO DELETE
	JEQ  BADNUM
	MOV  *R5,R5
	JLT  BADNUM
	JEQ  BADNUM
	CI   R5,32
	JGT  BADNUM
	UNL
	IF   GENEVE
	LIST
	LI   R1,SPRNUM
	MOV  R5,*R1
	LI   R0,SPRDEL		DELETE SPRITE FUNCTION
	LI   R2,1		# OF SPRITES TO DELETE
	XOP  @DVIDXP,0
	ELSE
	LIST
	DEC  R5
	SLA  R5,2
	LI   R0,SPRVDP
	A    R5,R0
	LIMI 0
	LI   R1,ATRLIS    DEFAULT ATTRIBUTE LIST
	LI   R2,4
	BLWP @CVMBW$	WRITE IT
	LI   R0,SPMOTI
	A    R5,R0
	LI   R1,MOTLIS
	LI   R2,4
	BLWP @CVMBW$
	LIMI 2
	ENDIF
	JMP  SPRRET
*
SET40E EQU  $
	BLWP *R10
	DATA ERROR
	TEXT 'S4'	40 COLUMN MODE ERROR
	JMP  SPRRET
*
BADNUM EQU  $
	BLWP *R10
	DATA ERROR
	TEXT 'SR'	BAD SPRITE NUMBER (1 TO 32)
*
SPRRET EQU  $
	MOV  @TEMPS,R3
	B    *R11
*
*  DATA AREA
*
TEMPS	BSS  4
ANUMB	BSS  2	  		SPRITE NUMBER ( 1 TO 32 )
*
	UNL
	IF   GENEVE
	LIST
DVIDXP	DATA VIDXOP
	ELSE
	LIST
ATRLIS	DATA >D000,>0000  	DEFAULT SPRITE ATTRIBUTE
MOTLIS	DATA >0000,>0000
	ENDIF
	END
