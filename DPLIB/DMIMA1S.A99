       TITL 'Double Precision Maximum Routine'
       IDT  'DMIMA1'
*****************************************************
* DMAX1, DMIN1 FORTRAN FUNCTION IN ASSY , 10 MAX ARGS
*****************************************************
*
*  Copyright 1988 by LGMA Products
*
*  Change History:
*
*  07-August-87   Version 3.0    Version from Elmer Clausen.  Various
*			         changes made to streamline & reduce
*				 code requirements.  Title and IDNT
*				 added.  Set status added before return.
*
*  09-Sept-88     Version 4.0    MDOS Compatible Version
*
	DEF  DMAX1
	DEF  DMIN1
*
	REF  DCMP$		REFERENCE FLOATING COMPARE ROUTINE
*
DMIN1	EQU  $
	DATA -10		10 ARGUMENTS MAXIMUM
	DATA BASEAD		BASE ADDRESS
	SETO @COMPFL		DO MINIMUM COMPARE
	JMP  DCONT
*
DMAX1	EQU  $
	DATA -10		10 ARGUMENTS MAXIMUM
	DATA BASEAD
	CLR  @COMPFL		DO MAXIMUM COMPARE
DCONT	EQU  $
	CLR  @ARGNUM		CLEAR ARGUMENT NUMBER
	BL   @LOADAR		GET ARGUMENT POINTER
	MOV  *R2+,R5		GET FIRST ARGUMENT
	MOV  *R2+,R6
	MOV  *R2+,R7
	MOV  *R2,R8
ARGLOP	EQU  $
	INC  @ARGNUM
	BL   @LOADAR		GET 2ND ARGUMENT NUMBER
	MOV  R2,R2		IS THIS ARGUMENT HERE?
	JEQ  ARGDON		BRIF NO, ZERO ARGUMENT ADDRESS
	ABS  @COMPFL		GET SENSE OF COMPARE
	JEQ  ARGMAX		DO MAXIMUM COMPARE
	BL   @DCMP$		ELSE, DO MINIMUM COMPARE
	JGT  ARGMOV		IF GREATER THAN, MUST MOVE ARGUMENT
	JMP  ARGLOP
ARGMAX	EQU  $
	BL   @DCMP$		COMPARE WITH FAC
	JLT  ARGMOV		IF LESS THAN, SKIP
	JMP  ARGLOP		IF EQUAL, SKIP
ARGMOV	EQU  $
	BL   @LOADAR		ELSE, LOAD ARGUMENT ADDRESS AGAIN
	MOV  *R2+,R5
	MOV  *R2+,R6
	MOV  *R2+,R7
	MOV  *R2,R8
	JMP  ARGLOP
*
ARGDON	EQU  $
	MOV  @BASEAD,R3		RESTORE BASE
	MOV  @RETUAD,R11	RETURN ADDRESS
	MOV  R5,R5		SET STATUS
	B    *R11		AND RETURN
*
*  LOADAR : LOAD R2 WITH ADDRESS OF ARGUMENT, BASED ON THE ARGUMENT
*	    NUMBER IN VARIABLE ARGNUM.
*
LOADAR	EQU  $
	CLR  R2
	MOV  @ARGNUM,R1		GET ARGUMENT NUMBER
	CI   R1,10
	JEQ  ARGDON		BRIF EXCEEDED MAXIMUM ARGUMENT NUMBER
	SLA  R1,1		* 2
	MOV  @VAL(R1),R2	GET ADDRESS
LOADZE	EQU  $
	B    *R11		AND RETURN
*
BASEAD	BSS  2			TEMP DATA ADDRESS
RETUAD	BSS  2			RETURN ADDRESS
VAL	BSS  20			10 FP VALUE ADDRESSES
ARGNUM	BSS  2			ARGUMENT NUMBER (0-9)
COMPFL	BSS  2			SENSE OF COMPARE FLAG
	END
