*
* PROGRAM FOR EXPERIMENTING WITH MOUSE IN GRAPHICS 6 MODE
* 1989 BRUCE HELLSTROM
*
       DEF  START
       DEF  SEND
*
START  B    @MAIN       *ENTRY POINT
VID    DATA >0006       *VIDEO XOP
RESMS  DATA >0000
UWS    EQU  >F000       *XOP WORKSPACE
DATBUF EQU  >E000       *XOP DATA BUFFER
SPLOC  EQU  >E200       *SPRITE LOCATION DATA FIRST WORD IS SPRITE #
MSY    EQU  >E202       *SECOND WORD IS PIXEL ROW
MSX    EQU  >E204       *THIRD WORD IS PIXEL COLUMN
CHRDAT DATA >80C0,>E0F0,>C080,>0000     *SPRITE CHAR PATTERN
SPRDAT DATA >0000       *SPRITE DATA 1ST WORD IS SPRITE #
       DATA >00FC       *NEXT WORD IS CHAR NUMBER (252)
       DATA >0064       *NEXT WORD IS PIXEL ROW
       DATA >0064       *NEXT WORD IS PIXEL COL
       DATA >0000       *NEXT 2 WORDS ARE VELOCITY
       DATA >0000
       DATA >0006,>0006,>0006,>0006    *COLOR DATA (ONE WORD PER LINE
       DATA >0006,>0006,>0006,>0006    *OF SPRITE)
*
VWS    BSS  >20         *VIDEO WS FOR VWTR ROUTINE
H00    BYTE >00
BUT1   BYTE >00         *FLAGS FOR MOUSE BUTTONS
BUT2   BYTE >00
BUT3   BYTE >00
       EVEN
*
MAIN   LWPI UWS          *SET VIDEO MODE TO GRAPH 6
       CLR  R0
       LI   R1,>0008
       XOP  @VID,0
*
       LI   R0,>07F4     *SET TEXT COLOR
       LIMI 0
       BLWP @VWTR
       LIMI 2
*
       LI   R0,8         *MOVE PATTERN DATA TO DATA BUFFER FOR XOP
       LI   R1,CHRDAT
       LI   R2,DATBUF
LOOP4  MOVB *R1+,*R2+
       DEC  R0
       JNE  LOOP4
*
       LI   R0,>0023     *SET PATTERN FOR SPRITE CHAR 252
       LI   R1,DATBUF    *NOT THE SAME XOP AS SETTING FOR A TEXT CHAR
       LI   R2,1
       LI   R3,>00FC
       CLR  R4
       XOP  @VID,0
*
       LI   R0,26        *MOVE THE SPRITE DATA UP TO XOP DATA BUFFER
       LI   R1,SPRDAT
       LI   R2,DATBUF
LOOP2  MOVB *R1+,*R2+
       DEC  R0
       JNE  LOOP2
*
       CLR  @SPLOC	 * SET SPRITE 0
*
       LI   R0,>001A     *DEFINE SPRITE
       LI   R1,DATBUF
       LI   R2,1
       XOP  @VID,0
*
*
       LI   R0,>0030     *SET MOUSE AT 100,100 SPEED 0
       LI   R1,>0064
       LI   R2,>0064
       LI   R3,>0000
       XOP  @VID,0
*
TLOOP  BL   @MKEY        *CHECK FOR MOVEMENT OR PRESS OF MOUSE BUTTON 1
*
       MOVB @BUT1,@BUT1  *BUTTON 1 DOWN?
       JEQ  TLOOP        *NO KEEP LOOPING
*
       CLR  R0           *ELSE WE ARE DONE
       LI   R1,>000A
       XOP  @VID,0
       BLWP @>0000
*
VWTR   DATA VWS,VWTR1    *VECTOR FOR VWTR ROUTINE
*
VWTR1  MOV  *R13,R1      *SIMPLE VWTR ROUTINE
       SWPB R1
       MOVB R1,@>F102
       SWPB R1
       ORI  R1,>8000
       MOVB R1,@>F102
       RTWP
*
MKEY   MOV  R11,R10      *SUBROUTINE FOR CHECKING THE MOUSE INPUT
       CLR  R5           *FOR COMPARING TO ZERO
       CLR  @RESMS       *FLAG IN CASE WE NEED TO RESET MOUSE
       LI   R6,>0200     *MAXIMUM PIXEL COL +1
       LI   R7,>00D4     *MAXIMUM PIXEL ROW +1
       LI   R0,>0031     *GET ABSOLUTE MOUSE POSITION AND BUTTON STATUS
       XOP  @VID,0
       C    R1,R5        *CHECK FOR MOUSE X>0
       JGT  NCHK         *YES GOTO NEXT CHK
       CLR  R1           *NO RESET TO 0
       INC  @RESMS       *SET RESET FLAG
       JMP  NCHK1
NCHK   C    R1,R6        *CHECK FOR MOUSE X<MAX COL
       JLT  NCHK1        *YES GOTO NEXT CHK
       LI   R1,>01FF     *NO RESET TO MAX COLUMN
       INC  @RESMS       *SET RESET FLAG
*
NCHK1  C    R2,R5        *CHECK FOR MOUSE Y>0
       JGT  NCHK2        *YES GOTO NEXT CHK
       CLR  R2           *RESET TO 0
       INC  @RESMS       *SET RESET FLAG
       JMP  RSET
NCHK2  C    R2,R7        *CHECK FOR MOUSE Y<MAX ROW
       JLT  RSET         *YES GOTO RESET CHECK
       LI   R2,>00D3     *SET TO MAX ROW
       INC  @RESMS       *SET RESET FLAG
*
RSET   MOV  @RESMS,@RESMS   *DO WE NEED TO RESET MOUSE POS?
       JEQ  MCON1           *NO SO PROCEED
       LI   R0,>0030        *YES SO RESET MOUSE POSITION
       MOV  R3,R4           *SAVE BUTTON STATUS
       CLR  R3              *KEEP SPEED 0
       XOP  @VID,0
       MOV  R4,R3           *REPLACE BUTTON STATUS
*
MCON1  MOV  R1,@MSX         *MOVE TO SPRITE LOCATION DATA
       MOV  R2,@MSY         *SAME
       MOV  R3,R4           *GET BUTTON STATUS AND SET APPROPRIATE FLAGS
       ANDI R4,>8000
       MOVB R4,@BUT1        *THE ABOVE MOUSE RESETTING ROUTINE IS NECESSARY
       MOV  R3,R4           *BECAUSE THE MOUSE POSITION RETURNED IS NOT
       ANDI R4,>4000        *LIMITED TO THE SCREEN DIMENSIONS WHICH CAUSES THE
       MOVB R4,@BUT2        *THE SPRITE TO WRAP IF THE DIMENSIONS ARE EXCEEDED
       MOV  R3,R4
       ANDI R4,>2000
       MOVB R4,@BUT3
       LI   R0,>001C        *RESET SPRITE POSITION
       LI   R1,SPLOC
       LI   R2,1
       XOP  @VID,0
       B    *R10            *RETURN
SEND   EQU  $
       END
