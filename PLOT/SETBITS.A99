	TITL  'HI-RES SUPPORT'
	IDT   'PLOTTR'
*
	DEF   SETBIT
*
	COPY  "FORTPAR:"
*
	COPY  "EQUATES:FORTEX.EQ9"
*
*  SETBIT  -  PUT SCREEN IN BITMAP MODE
*
SETBIT	EQU  $
	DATA 0			NO ARGUMENTS
	DATA BASEAD		TEMPDATA AREA
*
*  SET SCREEN TO A NON-BITMAPPED MODE, OR THIS WON'T WORK
*
	LI   R1,0		SET IT TO 40 CHAR MODE
	BLWP @CSCRMO
*
*  FIRST, OPEN ONLY 1 DISK FILE TO REDUCE VDP RAM USAGE
*
	LI   R0,>3100		OPEN ONLY 1 FILE
	BLWP @CFILE$		OPEN THAT MANY FILES
*
*  REOPEN CRT AS FILE 6
*
	LI   R0,6		OPEN CRT AS FILE 6
	LI   R1,CRTPAB
	BLWP @COPFI$
*
*  CLEAR UP ANY SPRITES
*
	CLR  R0
	MOVB R0,@NUMSPR		NUMBER OF SPRITES IN MOTION = 0
*
*  SAVE ASCII CHARACTER SET (SINCE I WILL BE MAPPING MY OWN)
*
	LI   R0,>0900		WHERE CURRENT SET IS MAPPED
	LI   R1,CHARSE
	LI   R2,768
	BLWP @CVMBR$		READ THEM
*
*  SET VR1 TO VDP REGISTER 1
*
	LI   R4,SCRMOB
	MOVB @17(R4),R0
	MOVB R0,@REFSCR		TO KEEP GPL HAPPY (AND NOT LOOSE SPRITE MAGNIFY)
*
*  LOAD UP THE BITMAP WORKSPACE
*
	LI   R4,SCRMOB
	LI   R1,SCRMOB+16
	CLR  R0
	LI   R2,8
VDPW1	EQU  $
	SWPB R0
	MOVB *R1+,R0
	SWPB R0
	BLWP @CVWTR$		WRITE THE REGISTER
	AI   R0,>1000
	DEC  R2
	JNE  VDPW1
*
*  SET UP THE SCREEN IMAGE TABLE
*
	MOV  @8(R4),R0		GET SCREEN IMAGE TABLE START
	CLR  R2
L1	EQU  $
	CLR  R1
L2	EQU  $
	BLWP @CVSBW$
	INC  R0
	AI   R1,>0100
	CI   R1,0
	JNE  L2
	INC  R2
	CI   R2,3
	JNE  L1
*
*  CLEAR THE PATTERN TABLE (BITMAPPED)
*
	MOV  @6(R4),R0		GET START ADDRESS OF TABLE
	CLR  R1			AND FILL COLOR TABLE
L3	EQU  $
	BLWP @VSBW
	INC  R0
	CI   R0,>1800
	JNE  L3
*
*  FILL THE COLOR TABLE.  USE DEFAULT COLOR CODE IN MENU
*
	MOV  @4(R4),R0		COLOR TABLE START
	MOVB @COLDEF,R1		DEFAULT COLOR DEFINITION
L4	EQU  $
	BLWP @CVSBW$
	INC  R0
	CI   R0,>3800
	JNE  L4
*
*  DISABLE ALL SPRITES
*
	MOV  @10(R4),R0		DISABLE SPRITES
	LI   R1,>D000
	BLWP @CVSBW$
*
*  SET SCREEN MODE SO WORLD WILL KNOW
*
	LI   R0,4
	MOV  R0,@SET40F		IN 40 FLAG
*
	MOV  R4,@TABADD		SET TABLE ADDRESS FOR SCREEN DESCRIPTOR
*
	MOV  @BASEAD,R3   	RESTORE BASE R3
	B    *R11	  	RETURN
*
BASEAD	BSS  2	     		CALLER'S R3
RETUAD	BSS  2	     		RETURN ADDRESS
*
SCRMOB	EQU  $
	DATA 32			00 32
	DATA 24*32		02 END OF SCREEN ADDRESS
*
	DATA >2000		04 COLOR TABLE START
	DATA >0000		06 PATTERN TABLE
	DATA >1800		08 SCREEN IMAGE TABLE
	DATA >1B00		0A SPRITE ATTRIBUTE TABLE
	DATA >0000		0C SPRITE PATTERN TABLE
	DATA >0000		0E SPRITE MOTION TABLE
*
	DATA >02E0		10 00 01 BIT MAP MODE
	DATA >06FF		12 02 03 SCREEN IMAGE TABLE, COLOR TABLE
	DATA >0306		14 04 05
	DATA >00F5		16 06 07
*
CRTPAB	EQU  $
	BYTE 0,3,0,0,80,0,0,0,0,2
	TEXT 'CR'
	EVEN
*
*  CHARACTER SET; FOR ASCII SET ONLY (>20 TO >7F)
*
CHARSE	BSS  768
*
	END
