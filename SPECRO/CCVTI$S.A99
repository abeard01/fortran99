	TITL    'COMPLEX CONVERSIONS (INTEGER TO FLOATING POINT)'
	IDT     'CCVIF$'
*
*  COMPLEX *8 AND COMPLEX *16 CONVERSIONS
*
*  	ICCVT$ - I*2 TO C*8
*  	IECVT$ - I*2 TO C*16
*  	KCCVT$ - I*2 TO C*8
*  	KECVT$ - I*2 TO C*16
*  	JCCVT$ - I*4 TO C*8
*  	JECVT$ - I*4 TO C*16
*
*  COPYRIGHT 1989 BY LGMA PRODUCTS
*
	DEF  ICCVT$
	DEF  IECVT$
	DEF  KCCVT$
	DEF  KECVT$
	DEF  JCCVT$
	DEF  JECVT$
*
	REF  JDCVT$		; I*4 TO R*8 CONVERSION
	REF  FACIM$		; IMAGINARY PORTION OF FAC
*
KCCVT$	EQU  $
	SRA  R5,8		; CONVERT I*1 TO I*2
*
ICCVT$	EQU  $
	MOV  R5,R6
	SRA  R5,15		; CONVERT I*2 TO I*4
*
JCCVT$	EQU  $
	MOV  R11,@SAVRET	; SAVE RETURN VECTOR
	BL   @JDCVT$		; CONVERT TO REAL *8
*
	CLR  R7			; CLEAR D.P. EXTENSION
	CLR  R8
	MOV  @SAVRET,R11	; RESTORE RETURN
	B    *R11		; AND RETURN
*
KECVT$	EQU  $
	SRA  R5,8		; CONVERT I*1 TO I*2
*
IECVT$	EQU  $			; CONVERT I*2 TO I*4
	MOV  R5,R6
	SRA  R5,15
*
JECVT$	EQU  $
	MOV  R11,@SAVRET	; SAVE RETURN VECTOR
	BL   @JDCVT$		; CONVERT TO DOUBLE PRECISION
	LI   R0,FACIM$		; CLEAR IMAGINARY PORTION
	CLR  *R0+
	CLR  *R0+
	CLR  *R0+
	CLR  *R0
	MOV  @SAVRET,R11
	B    *R11
*
SAVRET	BSS  2			; RETURN VECTOR SAVE
	END
